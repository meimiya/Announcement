[bg storage=d2.jpg time=0]
[black_in]





[iscript]
f.m = [
    ';「うひゃあああぁぁぁぁぁぁぁ！！」',
    '　私たちを乗せた四人乗りセダンは、深夜の波止場を猛スピードで疾走する。',
    ';速度は上がる一方だ――何しろ、アクセルが全開で固定されているのだから。',
    ';「クソッ！　俺たち本当に死んじまうのかよ！」',
    '　私の前に座る不良が毒づいた。',
    ';「うわぁぁぁもう駄目だぁぁぁぁぁ！！」',
    ';　この中で最年長である先輩が、情けない声を上げながら両腕を振り回す。',
    'その右腕が不良の顔に当たり、彼の怒りを爆発させた。',
    ';「ってーなぁこの野郎！　',
    ';テメェのせいでこうなってんだぞ！」',
    ';　怒鳴りながらも、不良の顔は正面を向いたままだ。',
    '彼は昨晩寝違えて首が回らないらしい。',
    '　それだけでなく、私たち全員の背中は特殊な接着剤でシートに固定されている。',
    ';このまま車が冬の海に転落すれば、身動きが取れない乗員が溺死した後で接着剤は溶解する。',
    '全員行儀よくシートベルトを締めたまま海に突っ込んだ事故現場の完成ってわけだ。',
    ';――私たちをこんな目に合わせたヤクザがそう丁寧に説明してくれた。',
    ';「海が近づいてくる！　死ぬ、死んじまうぅぅ！！」',
    '　先輩はまた喚き散らし、バックミラーを叩き折った。',
    ';「こ、こわいよターくん……」',
    ';　まだ十代前半の少女が涙目で訴える。',
    'ターくんと呼ばれた不良は強張った顔で少女の目を見つめ、大丈夫さ、と震える声で答えた。',
    '私は隣のシートに置かれた２０インチのブラウン管テレビに目をやった。',
    ';そう、全てはこのテレビから始まったのだ……。',
]
f.m_length = f.m.length
f.page = [
    [0, 9],
    [10, 18],
    [19, 40],
]
f.m_4nin = "　車内にいるのは最大でも四人。";
f.m_seki = "　不良は運転席、先輩は助手席、私は運転席の後ろに座っている。";
f.m_shoumen = "　私の前の不良は常に正面を向いている。";
f.m_furyo_koubu = "　私の前の不良は後部座席を見ることができない。";
f.m_betsu = "　「私の前の不良」と「ターくんと呼ばれた不良」は別人。";
[endscript]

[current layer=message0]

;[jump target=think1]

[line i=0]
[line i=1]
[line i=2]
[line i=3]
[line i=4]
[line i=5]
[line i=6]
[line i=7]
[line i=8]
[line i=9]
[line i=10]
[line i=11]
[line i=12]
[line i=13]
[line i=14]
[line i=15]
[line i=16]
[line i=17]
[line i=18]
[line i=19]
[line i=20]
[line i=21]
[line i=22][cm]

*loop2
[mes_open face="🥴"]
……[p]
何だこの状況！[p]
[mes_open face=""]
ワタシはノベルデバッガー。[l]小説の中の矛盾を見つけるのが仕事だ。[p]
当局の出版物にはどれほど些細な矛盾も存在してはならない。[p]
物語自体は、荒唐無稽で現実にはありえないような内容でも出版を許可される。[p]
しかし、矛盾を含む小説は国民に現実への疑いを抱かせるものとして禁じられており、出版した場合は厳しく処罰される。[p]
[mes_open face="😔"]
かつていた同僚たちも、些細な矛盾を見落としたばかりに『処罰』され、今や校正者はワタシ一人……。[p]
[mes_open face="😠"]
二度と間違いを犯してはならない。[l]我々は常に完璧でなければならないのだ。[p]
[mes_close]
[line i=0]
[mes_open face="🙂"]
今回上流工程から送られてきたのは、「ノーブレーキ・トゥ・ヘブン」という小説の抜粋だ。[p]
[mes_close]
[line i=1]
[line i=2]
[line i=3]
[line i=4]
[line i=5]
[line i=6]
[line i=7]
[mes_open face="🤔"]
男女が小さな車に乗せられ、海へ突っ込んでいく様子が描かれているけど……これは物語の冒頭なのかな、それとも結末なのかな？[p]
[mes_open face="🤨"]
前後を読むことは許されていないけど、のっぴきならない状況らしいってのは確かだ。[p]
[mes_close]
[line i=8]
[line i=9]
[line i=10]
[line i=11]
[line i=12]
[line i=13]
[line i=14]
[line i=15]
[mes_open face="😵"]
ヤクザて。[p]
[mes_open face="🤔"]
〈私〉たちが何か企んで、悪い奴らに捕まって、始末されようとしてる……って感じ？[p]
[mes_open face="😑"]
さーて、どこに矛盾があるのかな？[p]
[mes_close]
[line i=16]
[line i=17]
[line i=18]
[line i=19]
[line i=20]
[line i=21]
[line i=22]
[mes_open face="😓"]
どんな事情があったらテレビを発端に命を奪われるんだよ。いい加減にしろ。[p]
[mes_open face="😐"]
とまぁ、内容に突っ込んでも仕方ないわけで……。[p]
[mes_open face="🤫"]
荒唐無稽だけど、明らかに間違ってるって場所は見当たらないなぁ。[p]
[mes_open face="🙂"]
今回はちょっと長いから、わからなくても適当にいくつかピックアップして考察してみよう。[p]
[mes_open face="😊"]
色々な文を組み合わせているうちに、何か思いつくかもしれないしね。[p]
[mes_close]
[cm]

*think1
[mes_close]
[think count_max=2 storage=scene2.ks answer=think1_answer note=think1_note]

*think1_note
[mes_open face="🤔"]
この小説で気になるところと言えば……。[p]
前半で「不良」は先輩に対してキレているけど、後半では少女に大丈夫さと話しかけてる。[p]
[mes_open face="😦"]
なんだか性格が噛み合わない気がする。この辺りに矛盾の香りが漂うね。[p]
[if exp="f.m.indexOf(f.m_seki) != -1"]
    [mes_open face="🙂"]
    [image layer=0 visible=true page=fore storage=d2_1.svg top=10 left=350]
    車内の席順は恐らくこんな感じだ。[p]
    [freeimage layer=0]
    [layopt layer=0 visible=false]
[endif]
[jump target=think1]

*think1_answer
;   ゴール
[if exp="hit_count([f.m_betsu, f.m_4nin]) == 2"]
    [jump target=goal]
[endif]
;   テレビ、シートベルト、四人乗りの推理
[if exp="hit_count([1, 14]) == 2 && f.m.indexOf(f.m_4nin) == -1"]
    [mes_open face="🤔" extra=true]
    全員がシートベルトを締めている……。[p]
    [mes_open face="🤔"]
    四人乗りの車でも、後部座席には三人分のシートベルトが備え付けてあったりするよね。この車はわからないけど。[p]
    [mes_open face="🙄"]
    少なくとも、この車内にいる人物は最大で五人ってことは言えるね。[p]
    乗員の数は鍵になりそうな気がする……。[p]
    [mes_open face="🤔"]
    他に乗員の数を示すような記述はあったかな？[p]
    乗車できる人数に関連する記述は？[r]
    [link_hover target=select1_failed text="その右腕が不良の顔に当たり……"]
    [link_hover target=select1_failed text="全員の背中は特殊な接着剤でシートに固定……"]
    [link_hover target=select1_success text="２０インチのブラウン管テレビ……"]
    [s]
    *select1_failed
    [mes_open face="😔"]
    むぅ……？　何か勘違いしてる？[p]
    [jump target=think1]
    *select1_success
    [mes_open face="😯"]
    そうだ！　テレビが座席を占領しているんだった。[p]
    [mes_open face="🤔"]
    よっぽど細い人が集まれば、テレビと三人が横並びで座ることも不可能じゃないかもしれないけど……。[p]
    その状態で全員きちんとシートベルトを締めるのはさすがに無理だよね。[p]
    [mes_open face="😜"]
    ってことは後部座席に座れるのは最大でも二人。[l]よって乗員は最大でも四人しかいないはずだ。[p]
    これは新事実として追記しておこう。[p]
    [eval exp="f.p = 2;f.m.push(f.m_4nin)"]
    [jump target=think1]
[endif]
[if exp="hit_count([1, 21]) == 2 && f.m.indexOf(f.m_4nin) == -1"]
    [mes_open face="😯" extra=true]
    ２０インチのテレビが座席に置いてある……。[p]
    [mes_open face="😯"]
    ２０インチって言ったら、多分私よりも横幅があるよね。[p]
    四人乗りセダンの後部座席にテレビが置いてあったら、そこに何人まで乗れるだろう。[p]
    [mes_open face="🤔"]
    ガリガリの人を三人集めれば不可能とまでは言えないかもね。[l]体形については何も述べられてないし。[p]
    [mes_open face="😐"]
    それともどこかに「テレビと三人が横並びに座れるかどうか」を示す記述はあったっけ？[p]
    「テレビと三人が座れるかどうか」を示す記述は？[r]
    [link_hover target=select2_success text="全員行儀よくシートベルトを締めたまま……"]
    [link_hover target=select2_failed text="不良は強張った顔で少女の目を見つめ……"]
    [link_hover target=select2_failed text="私の前に座る不良が……"]
    [s]
    *select2_failed
    [mes_open face="😔"]
    むぅ……？　何か勘違いしてる？[p]
    [jump target=think1]
    *select2_success
    [mes_open face="😉"]
    これだ！[p]
    仮に後部座席にテレビと三人が横並びに座れたとしても、その三人が全員シートベルトを締めるのはさすがにきついはずだ。[p]
    つまり車内にいるのは最大でも四人まで。[l]これは矛盾を構成する足がかりになりそう。[p]
    よし、新事実として追記しておこう。[p]
    [eval exp="f.p = 2;f.m.push(f.m_4nin)"]
    [jump target=think1]
[endif]
;   テレビ、シートベルト、四人乗りのヒント
[if exp="hit_count([1]) == 1 && f.m.indexOf(f.m_4nin) == -1"]
    [mes_open face="🤔"]
    四人乗りセダン……。[p]
    普通のセダンは後部座席に三人乗れるよね。[p]
    つまりこの車には五人まで乗れる。頑張ればもっと乗れるかも。[p]
    けど、乗員の数を制限するような記述があった気がするな……。[p]
    [jump target=think1]
[endif]
[if exp="hit_count([14]) == 1 && f.m.indexOf(f.m_4nin) == -1"]
    [mes_open face="🤔"]
    全員シートベルト着用か……。[p]
    そもそもこの車、何人乗りなんだっけ？　それによってシートベルトの数も変わってくるよね。[p]
    [jump target=think1]
[endif]
[if exp="hit_count([21]) == 1 && f.m.indexOf(f.m_4nin) == -1"]
    [mes_open face="🤔"]
    テレビが座席にあったら邪魔だなぁ。足下に置けばいいのに。[p]
    それはともかく、テレビが座席にあることで他に何か影響を与えていないかな？[p]
    [jump target=think1]
[endif]

[if exp="hit_count([4, 7]) == 2 && f.m.indexOf(f.m_seki) == -1"]
    [mes_open face="🤔" extra=true]
    私の前に不良の顔に、先輩の右腕が当たった……。[p]
    [image layer=0 visible=true page=fore storage=d2_1.svg top=10 left=350]
    [mes_open face="🤔"]
    ってことは、私は後部座席に座っていて、その前に先輩と不良がいる、と。[p]
    [mes_open face="😯"]
    この車が国産車なら、不良は運転席、先輩は助手席に座ってることになる。[p]
    [mes_open face="🙂"]
    少女の席については言及がないけど、運転席と助手席が埋まってるなら後部座席にいるはずだ。[p]
    この席の情報は役立つかもしれない。[l]追記しておこう。[p]
    [eval exp="f.p = 2;f.m.push(f.m_seki)"]
    [freeimage layer=0]
    [layopt layer=0 visible=false]
    [jump target=think1]
[endif]

;   二人の不良は別人
[if exp="hit_count([11, 12]) == 2 && f.m.indexOf(f.m_shoumen) == -1"]
    [mes_open face="🤔" extra=true]
    不良は首が回らなくて、その背中はシートに張り付けられている……。[p]
    [mes_open face="😯"]
    ってことは、私の前の不良は絶対に正面しか向けないってことだよね。[p]
    [mes_open face="🤔"]
    この情報、何かに使えるかなぁ。[l]まぁ一応追記しておこうか。[p]
    [eval exp="f.p = 2;f.m.push(f.m_shoumen)"]
    [jump target=think1]
[endif]

[if exp="hit_count([f.m_shoumen, 17]) == 2 && f.m.indexOf(f.m_furyo_koubu) == -1"]
    [mes_open face="🤨" extra=true]
    バックミラーが壊された……つまり、これ以降はミラー越しに後ろを見ることができなくなったはずだ。[p]
    [mes_open face="😑"]
    一方、不良は常に正面を向いている。[p]
    だとしたら、不良は絶対に後ろを見ることができないよね。[p]
    [mes_open face="🙂"]
    よし、追記しておこう。[p]
    [eval exp="f.p = 2;f.m.push(f.m_furyo_koubu)"]
    [jump target=think1]
[endif]

[if exp="hit_count([f.m_furyo_koubu, 20]) == 2 && f.m.indexOf(f.m_betsu) == -1"]
    [mes_open face="😯" extra=true]
    あっ！[p]
    [mes_open face="😯"]
    もし不良が一人しかいないなら、「私の前の不良」が少女を見つめたはずだ！[p]
    でも不良は正面しか見ることができない。[l]そこに少女がいるはずがない。[p]
    [mes_open face="😉"]
    まさか少女がボンネットに寝そべっていたわけもないしね。[p]
    [mes_open face="😃"]
    つまり彼は「ターくん」と呼ばれた不良とは別人。この場に「不良」は二人いるんだ。[p]
    [mes_open face="🙂"]
    これは追記すべきだね。[l]ゴールは近い気がする。[p]
    [eval exp="f.p = 2;f.m.push(f.m_betsu)"]
    [jump target=think1]
[endif]

;   二人の不良は別人のヒント
[if exp="hit_count([f.m_4nin]) == 1"]
    [mes_open face="🤔"]
    車内にいるのは最大でも四人……。[p]
    [mes_open face="🙂"]
    何とかしてこの場に五人以上いることを示せないかな？[p]
    そうすれば一発で矛盾って言えるんだけどなぁ。[p]
    [jump target=think1]
[endif]
[if exp="hit_count([17]) == 1 && f.m.indexOf(f.m_furyo_koubu) == -1"]
    [mes_open face="🤔"]
    バックミラーが壊された……？[p]
    これによって車内に何か変化は起きていないかな。[p]
    [jump target=think1]
[endif]
[if exp="hit_count([f.m_shoumen]) == 1 && f.m.indexOf(f.m_furyo_koubu) == -1"]
    [mes_open face="🤔"]
    私の前の不良はずっと正面を向いている……。[p]
    ってことは、彼は「少女を見つめる不良」とは別人なのかな？[p]
    いや、後ろを向けないからって後ろを見れないとは限らないよね。[p]
    運転するとき、後方確認のためにいちいち後ろを振り向いたりしないし。[p]
    [jump target=think1]
[endif]
[if exp="hit_count([f.m_furyo_koubu]) == 1 && f.m.indexOf(f.m_betsu) == -1"]
    [mes_open face="🤔"]
    私の前の不良は後部座席を見れない……？[p]
    [mes_open face="😲"]
    ってことは、この不良にはあのことが不可能だったはずだ！[p]
    [jump target=think1]
[endif]
[if exp="f.m.indexOf(f.m_betsu) == -1"]
    [if exp="hit_count([4]) == 1 || hit_count([20]) == 1"]
        [mes_open face="🤔"]
        そういえば、不良って呼ばれている人物に関する言及が多い気がする。[p]
        先輩に対してキレて、少女に大丈夫さと話しかけてるけど……[p]
        [mes_open face="😦"]
        なんだか不良の性格がちぐはぐな気がする。[p]
        [mes_open face="😯"]
        ん？　もしかして不良って二人いるのかな？[p]
        名前のわからない人物に対して「〇〇な人」「〇〇の女」みたいに描写することがあるけど、この不良も別人で……[p]
        「私の前に座る不良」と「ターくんと呼ばれた不良」の二人が乗車している？[p]
        [mes_open face="🤔"]
        いや、一人とも取れる書き方だよなぁ。うーん、どうなんだろ。[p]
        [jump target=think1]
    [endif]
[endif]

;   ノーヒット
[mes_open face="😑"]
……。[p]
駄目だ。何も思いつかない。[p]
[jump target=think1]

*goal
[flash]
[mes_open face="😑" extra=true]
……。[p]
[mes_open face="😉"]
肝心なのは、「車に何人乗っているか？」って点だ。[p]
パッと見、登場人物は〈私〉、不良、先輩、そして少女の四人に思える。[p]
[mes_open face="😯"]
けど、「ターくんと呼ばれた不良」と「私の前に座る不良」は別人のようにも読める。[p]
[mes_open face="🙂"]
もし最初の「不良」が「ターくん」と別人なら、車には五人乗っていることになる。[p]
[pickupLines a="f.m[1]"]
[mes_open face="😏"]
でもそれは、この一文と食い違う。これがこの小説の矛盾点だ。[p]
[mes_open face="🙄"]
四人乗りのセダンって言っても、普通は後部座席に三人座れるから、五人乗せることもできるけど……[p]
[pickupLines a="f.m[14]" b="f.m[21]"]
[mes_open face="😏"]
この二つの文がその可能性を潰しているね。[p]
後部座席に三人分のシートベルトがあるセダンは珍しくないけど……[p]
[mes_open face="😌"]
仮に五人が乗っていて、後部座席の三人が全員シートベルトを締めていたとしたら、テレビの置き場所がない。[p]
20インチのブラウン管テレビなんて、私よりも横幅があるはずだ。まぁ実物を見たことはないけど。[p]
よって乗員が五人であること――「前の不良」と「ターくん」が別人であることを証明すれば、矛盾を導けるわけだ。[p]
[mes_open face="🤫"]
そのために、まずは乗員の座っている位置を考える。[p]
[pickupLines a="f.m[4]" b="f.m[7]"]
[mes_open face="😗"]
不良は〈私〉の前に座っていて、先輩の右腕が不良に当たった……。[p]
消去法で、少女は先輩の後ろの座席に座っているはずだ。[p]
[pickupLines a="f.m[20]"]
[mes_open face="😆"]
ここで重要になってくるのはこの文。[p]
[mes_open face="😃"]
もしターくんが〈私〉の前の不良のことなら、不良は斜め後ろの少女を見たってことになる。[p]
[pickupLines a="f.m[11]"]
[mes_open face="😑"]
でもそれは不可能だよね。何しろ不良は寝違えて首が回らないんだから。[p]
隣の先輩の方すら見れないのに、後ろの少女を見つめるなんてできるわけがない。[p]
[pickupLines a="f.m[17]"]
[mes_open face="😏"]
バックミラー越しに後部座席を見ることもできたけど、先輩がバックミラーを叩き折った後では不可能だ。[p]
以上から、少女を見つめた「ターくん」は五人目の乗員ってことになるけど……[p]
[mes_open face="😆"]
この車には四人までしか乗車できない――よって矛盾している！[p]
[mes_close]

[eval exp="openGoal('ノーブレーキ・トゥ・ヘブン', '車内の描写から推測される乗車人数と<br/>舞台となる小型車の定員数の間に<br/>矛盾を認める。<br/>適切に処理されたし。')"]
[s]

*finish
[black_out]
[cm]

[jump storage=first.ks target=*title]